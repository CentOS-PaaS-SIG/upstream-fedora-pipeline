#!groovy


timestamps {
    def libraries = ['ci-pipeline'             : ['master', 'https://github.com/CentOS-PaaS-SIG/ci-pipeline.git'],
                     'upstream-fedora-pipeline': ['master', 'https://github.com/CentOS-PaaS-SIG/upstream-fedora-pipeline.git']]

    libraries.each { name, repo ->
        library identifier: "${name}@${repo[0]}",
                retriever: modernSCM([$class: 'GitSCMSource',
                                      remote: repo[1]])

    }

    // Audit file for all messages sent.
    msgAuditFile = "messages/message-audit.json"

    // Number of times to keep retrying to make sure message is ingested
    // by datagrepper
    fedmsgRetryCount = 120

    properties(
            [
                    buildDiscarder(logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '500', daysToKeepStr: '', numToKeepStr: '500')),
                    parameters(
                            [
                                    string(description: 'fedora-fedmsg', defaultValue: '{}', name: 'CI_MESSAGE')
                            ]
                    ),
                    pipelineTriggers(
                            [
                                    [$class: 'CIBuildTrigger',
                                     checks: [
                                             [expectedValue: '1|CLOSED', field: 'new'],
                                             [expectedValue: '^(?!koschei).*', field: 'owner']
                                     ],
                                     providerName: 'fedora-fedmsg',
                                     selector: 'topic = "org.fedoraproject.prod.buildsys.build.state.change" OR topic = "org.fedoraproject.prod.buildsys.task.state.change"']
                            ]
                    )
            ]
    )

    def TRIGGER_RETRY_COUNT = 3
    def stepName = null

    node('master') {

        // pull in ciMetrics from ci-pipeline
        ciMetrics.prefix = packagepipelineUtils.influxDBPrefix()
        packagepipelineUtils.cimetrics = ciMetrics
        def jobMeasurement = packagepipelineUtils.timedMeasurement()

        packagepipelineUtils.ciPipeline {
            try {
                stepName = 'upstream-fedora-pipeline-build-trigger'
                stage(stepName) {

                    packagepipelineUtils.timedPipelineStep(stepName: stepName, debug: true) {

                        print "CI_MESSAGE"
                        print CI_MESSAGE
                        pipelineUtils.flattenJSON('fed', env.CI_MESSAGE)
                        // Get request vars from scratch messages
                        env.fed_request_0 = env.fed_request_0 ?: env.fed_info_request_0
                        env.fed_request_1 = env.fed_request_1 ?: env.fed_info_request_1
                        // Determine if scratch build
                        env.isScratch = false
                        if (env.fed_info_request_0) {
                            env.isScratch = true
                        }
                        packagepipelineUtils.setDefaultEnvVars()
                        targetBranch = false
                        if (env.fed_request_1) {
                            pipelineUtils.setBuildBranch(env.fed_request_1, "fed")
                            targetBranch = packagepipelineUtils.checkBranch()
                        }
                        // If the PR pipeline submit the build, let's just set targetBranch to false so we don't run
                        if (env.fed_owner == "bpeck/jenkins-continuous-infra.apps.ci.centos.org" || env.fed_owner == "koschei") {
                            print("This build is for a scratch build from the PR pipeline. Not triggering.")
                            targetBranch = false
                        }
                        if (targetBranch) {
                            pipelineUtils.repoFromRequest(env.fed_request_0, 'fed')
                            testsExist = pipelineUtils.checkTests(env.fed_repo, env.fed_branch, 'classic')
                            // Ensure message is from primary koji instance
                            primaryKoji = fed_instance == "primary"
                            pipelineUtils.setBuildDisplayAndDescription()
                        } else {
                            currentBuild.displayName = "Build#: ${env.BUILD_NUMBER} - Branch: ${env.branch} - Skipped"
                        }
                        pipelineUtils.initializeAuditFile(msgAuditFile)

                    }

                }

                if (targetBranch && testsExist && primaryKoji) {
                    messageFields = packagepipelineUtils.setMessageFields('package.queued', 'build')
                    pipelineUtils.sendMessageWithAudit(messageFields['topic'], messageFields['properties'], messageFields['content'], msgAuditFile, fedmsgRetryCount)
                    stepName = 'schedule build'
                    stage(stepName) {

                        retry(TRIGGER_RETRY_COUNT) {
                            packagepipelineUtils.timedPipelineStep(stepName: stepName, debug: true) {

                                build job: "fedora-${env.branch}-build-pipeline",
                                        // Scratch messages from task.state.changed call it id, not task_id
                                        parameters: [string(name: 'PROVIDED_KOJI_TASKID', value: env.fed_task_id ?: env.fed_id),
                                                     string(name: 'CI_MESSAGE', value: env.CI_MESSAGE)],
                                        wait: false
                            }
                        }
                    }

                } else {
                    echo "CI_MESSAGE was invalid. Skipping..."
                    messageFields = packagepipelineUtils.setMessageFields('package.ignored', 'build')
                    pipelineUtils.sendMessageWithAudit(messageFields['topic'], messageFields['properties'], messageFields['content'], msgAuditFile, fedmsgRetryCount)
                    currentBuild.description = "*Build Skipped*"
                }

                currentBuild.result = 'SUCCESS'

            } catch (e) {
                currentBuild.result = 'FAILURE'
                throw e
            } finally {
                // set the metrics we want
                ciMetrics.setMetricTag(jobMeasurement, 'package_name', env.fed_repo)
                ciMetrics.setMetricTag(jobMeasurement, 'build_result', currentBuild.result)
                ciMetrics.setMetricField(jobMeasurement, 'build_time', currentBuild.getDuration())
            }
        }
    }
}
